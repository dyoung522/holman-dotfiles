# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# Set the Theme here
POWERLEVEL9K_LEFT_PROMPT_ELEMENTS=(context dir rbenv nvm vcs)
ZSH_THEME="$ZSH/zsh/themes/powerlevel9k/powerlevel9k.zsh-theme"
source $ZSH_THEME

DEFAULT_USER=dcyoung

# your project folder that we can `c [tab]` to
export PROJECTS="~/Projects"

# create an empty chpwn_functions array
set -A chpwd_functions

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]] ; then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh} ; do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh} ; do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh} ; do
  source $file
done

# load syntax highlighting
if [[ -f "/usr/local/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh" ]] ; then
  source /usr/local/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi

unset config_files

# Load additional environment variables
[[ -f ~/.env ]] && source ~/.env
